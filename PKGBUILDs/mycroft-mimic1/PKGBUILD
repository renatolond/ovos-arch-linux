pkgbase=mycroft-mimic1
_gh_org=MycroftAI
_gh_proj=mimic1
pkgname=("mycroft-mimic1" "mycroft-mimic1-voices")
_commit="eba879c6e4ece50ca6de9b4966f2918ed89148bd"
pkgver="1.3.0"
pkgrel=1
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/MycroftAI/$_gh_proj"
license=('see https://github.com/MycroftAI/mimic1/blob/master/COPYING')
depends=()
makedepends=(gcc make pkg-config automake libtool)

source=("https://github.com/$_gh_org/$_gh_proj/archive/$_commit.tar.gz")
sha256sums=('3b0afee54dc4f2eef6e64d81d9d2373c090aab894993ef8921216a0147b9feff')

# prepare() {
#     cd "$srcdir"
#     for patch in "../"*.patch; do
#         if [ -f "$patch" ]; then
#           echo "Applying patch $patch"
#           patch --forward --strip=1 --input="${PWD}/${patch}" --directory="${srcdir}/${_gh_proj}-${_commit}"
#         fi
#     done
# }

build() {
    cd "$srcdir/$_gh_proj-$_commit"
    ./autogen.sh
    ./configure --enable-shared --prefix="/usr" #--with-audio=alsa
    make
}

package_mycroft-mimic1() {
    depends=(alsa-lib)
    pkgdesc='Mimic is a fast, lightweight Text-to-speech engine developed by Mycroft A.I. and VocaliD, based on Carnegie Mellon Universityâ€™s Flite (Festival-Lite) software. Mimic takes in text and reads it out loud to create a high quality voice.'

    cd "$srcdir/$_gh_proj-$_commit" 
    echo "Installing $pkgname to $pkgdir"
    make DESTDIR="$pkgdir" install

    if [ -d "$srcdir/$pkgname/docs" ]; then
        mkdir -p "$pkgdir/usr/share/doc/$pkgname"  # Create doc directory
        cp -r "$srcdir/$pkgname/docs" "$pkgdir/usr/share/doc/$pkgname" || true
        chmod 644 "$pkgdir/usr/share/doc/$pkgname"/*.txt  || true # Set read permissions for documentation files
    fi
    
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


package_mycroft-mimic1-voices() {
    arch=('any')
    pkgdesc='Flite engine voices for Mimic1'

    cd "$srcdir/$_gh_proj-$_commit" 
    echo "Installing $pkgname to $pkgdir"

    mkdir -p "${pkgdir}/usr/share/mimic/voices/"
    install -D -m644 ./voices/*.flitevox "${pkgdir}/usr/share/mimic/voices/"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
