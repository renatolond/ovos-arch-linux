_gh_org=OpenVoiceOS
_gh_proj=VocalFusionDriver
_pkgbase=ovos-enclosure-audio-vocalfusion
pkgname=$_pkgbase-dkms
pkgver='0.0.1'
_commit='924326c9ddefce240800a5b9d69345b99f1c358f'
pkgrel=4
pkgdesc='Raspberry Pi VocalFusion linux driver'
arch=('aarch64' 'armv7h')
url="https://github.com/$_gh_org/$_gh_proj/"
license=('Apache License 2.0')
depends=(dkms python-adafruit-blinka)
makedepends=(pkgconf dtc)
conflicts=("${_pkgbase}")
install=sj201.install

source=("https://github.com/$_gh_org/$_gh_proj/archive/$_commit.tar.gz" \
   Makefile app_xvf3510_int_spi_boot_v4_2_0.bin system.xvf3510.pa
   xvf3510-init.service xvf3510-init.sh xvf3510-flash dkms.conf sj201.install init_tas5806.py)
sha256sums=('b4892826ce0223f7928513249b07f91154baa584b7069a486a66059abaea06fb'
            'abff22f2091498baaa2f918eb777494878798c90ff6d792f30eabc440ff40d4c'
            'cfde2ac5174c7682414fa91f918346a3c57d495e7f6ddbfcbec5c222629bbf5a'
            '291e3286112bc295087663a83843e5245908951e560441e938a315c5fe8211c6'
            '6090980002ce787e3f73f01e8d2b856430eeb2627493cc966d517c2f730110a3'
            'e16b0dbf552424374db890f4630291b6c173b5ad1be17c08bba63fe22a76c539'
            'ca42c326ea96345477138333f23a20ffb03fdccc13948bf50c23053c673f5395'
            'cc24f4be8619b5834687ff8e050de565b833e5e224f61ddf24b12da51b9ae684'
            '59906e3379cffc5463e95b470b8b84e10fb02a57463e274c6f844e3bd385d8dd'
            '18d8752353a57590fa79eac5456ec7d6d558686f55614170eeed8ebf4b8d8434')

prepare() {
    cd "$srcdir"
    for patch in "../"*.patch; do
        if [ -f "$patch" ]; then
          echo "Applying patch $patch"
          patch --forward --strip=1 --input="${PWD}/${patch}" --directory="${srcdir}/${pkgname}-${pkgver}"
        fi
    done
}

build() {
    cd "$srcdir/$_gh_proj-$_commit"

    mkdir -p "$srcdir/build"
    dtc -@ -Hepapr -I dts -O dtb -o "$srcdir"/build/sj201.dtbo sj201.dts
}

package() {
    cd "$srcdir" 

    # Copy dkms.conf
    install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

    # Set name and version
    sed -e "s/@_PKGBASE@/${_pkgbase}/" \
        -e "s/@PKGVER@/${pkgver}/" \
        -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

    mkdir -p "$pkgdir/etc/pulse/system.pa.d"
    install -Dm644 system.xvf3510.pa "$pkgdir/etc/pulse/system.pa.d/"

    mkdir -p "$pkgdir/boot/overlays"
    install -Dm644 "./build/sj201.dtbo" "$pkgdir/boot/overlays"
    
    mkdir -p "$pkgdir/usr/lib/firmware/xvf3510"
    install -Dm644 "./app_xvf3510_int_spi_boot_v4_2_0.bin" "$pkgdir/usr/lib/firmware/xvf3510"

    mkdir -p "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "$srcdir/xvf3510-init.service" "$pkgdir/usr/lib/systemd/system"

    mkdir -p "$pkgdir/opt/ovos/bin"
    install -Dm744 "./xvf3510-flash" "$pkgdir/opt/ovos/bin"
    install -Dm744 "./xvf3510-init.sh" "$pkgdir/opt/ovos/bin"
    install -Dm744 "./init_tas5806.py" "$pkgdir/opt/ovos/bin"

    cp Makefile "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
    cp "$_gh_proj-$_commit"/driver/vocalfusion-soundcard.c "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
}
