_gh_org=OpenVoiceOS
_gh_proj=ovos-bus-server
pkgname=ovos-bus-server
_commit='6390bb787fad4660106577891ada6fa935b662d7'
pkgver=0.0.1
pkgrel=0
pkgdesc='alternative ovos bus service (websocket server) in C++'
arch=('x86_64' 'aarch64' 'armv7h')
url='https://github.com/OpenVoiceOS/ovos-bus-server/'
license=('Apache License 2.0')
depends=('ovos-service-base')
makedepends=(pkgconf cmake)
provides=("ovos-messagebus-server")
conflicts=("python-ovos-messagebus")

source=("https://github.com/$_gh_org/$_gh_proj/archive/$_commit.tar.gz" \
   0001-Make-package-installable.patch
   ovos-messagebus.service)
sha256sums=('2352676a4c1726d2febe7a39c920d5b8b26a5256913039c5e69beff5e5469d3e'
            'bbd1f91a15ba6f40358d618168d64547b1fac5f9aa53fea64ba7a2d0a0948b00'
            '53c5b43b05b5f4443ef28061124e244fc43b0d6e26dfeca106b59789d3189770')

prepare() {
    cd "$srcdir"
    for patch in "../"*.patch; do
        if [ -f "$patch" ]; then
          echo "Applying patch $patch"
          patch --forward --strip=1 --input="${PWD}/${patch}" --directory="${srcdir}/${pkgname}-${_commit}"
        fi
    done
}

build() {
    cd "$srcdir"

    build_dir=$(find . -type f -name "CMakeLists.txt" -exec dirname {} \; | awk 'length < shortest || shortest == 0 { shortest=length; shortest_path=$0 } END { print shortest_path }')
    echo "Build folder is $build_dir"
     
    cmake -B build -S "$build_dir"  \
        -DCMAKE_BUILD_TYPE='None' -DNDEBUG=ON \
        -DCMAKE_SKIP_INSTALL_RPATH=YES \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    cd "$srcdir" 
    echo "Installing $pkgname to $pkgdir"
    DESTDIR="$pkgdir" cmake --install build

    # Optional: Copy documentation files
    if [ -d "$srcdir/$pkgname/docs" ]; then
        mkdir -p "$pkgdir/usr/share/doc/$pkgname"  # Create doc directory
        cp -r "$srcdir/$pkgname/docs" "$pkgdir/usr/share/doc/$pkgname" || true
        chmod 644 "$pkgdir/usr/share/doc/$pkgname"/*.txt  || true # Set read permissions for documentation files
    fi

    # Search for license files in the original source directory
    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    find "$srcdir" -maxdepth 2 -type f -name 'LICENSE*' -exec cp -t "$pkgdir/usr/share/licenses/$pkgname" {} +

    mkdir -p "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "$srcdir/ovos-messagebus.service" "$pkgdir/usr/lib/systemd/system"
}